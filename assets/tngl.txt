defDevice($p400k, 0x00, 0xff, 25px, $PORTA, 0x00, 22px, $PORTB, 0x00, 43px, $PORTC, 0x00, {
  const button = ButtonProvider(0x0e);
  const boolean = Boolean();
  const negate = Negate();
  const event = EventRelay($light, 0xff);

  button->[0x00]boolean;
  boolean->[0x00]negate;
  negate->[0x00]event;
});

defSegment($_port, 0x00, { segment($PORTC, false); segment($PORTA, true); });

defSegment($front, 0x00, { segment($PORTB, true); });

defSegment($por_L, 0x00, { slice($_port, 0px, 24px, 1px); });

defSegment($por_T, 0x00, { slice($_port, 24px, 20px, 1px); });

defSegment($por_R, 0x00, { slice($_port, 44px, 24px, 1px); });

defSegment($porta, 0x00, { segment($PORTC, false); segment($PORTA, true); });

defCanvas($ALL, {
  segment($front);
  segment($porta);
});

defCanvas($TOP, {
  segment($por_T);
});

defCanvas($PORTA, {
  segment($porta);
});

defCanvas($FRONT, {
  segment($front);
});

// This is a comment
  // Constants
  var SMOOTING_LIGHT = 1s;
  var SMOOTING_TRANSITION = 1s;
  // Temporary stuff
  // Booting conditions
  catchEvent($INIT).emitAs($stand).setValue(100%).emitAs($brigh).emitAs($front).setValue(#000fff).emitAs($color).setValue(0%).emitAs($booti);
  catchEvent($INJEC).emitAs($stand).setValue(100%).emitAs($booti);
  catchEvent($CONNE).setValue(0%).emitAs($disco).setValue(0%).emitAs($booti);
  catchEvent($DISCO).setValue(100%).emitAs($disco).setValue(0%).emitAs($light);
  catchEvent($kread).emitAs($stand);
  catchEvent($kdisc).emitAs($SHUT);
  catchEvent($kshut).emitAs($SHUT).setValue(0%).emitAs($light);
  // Homing
  catchEvent($clean).emitAs($PROC).emitAs($_DONE);
  catchEvent($calib).emitAs($PROC).emitAs($_DONE);
  catchEvent($homin).emitAs($PROC).emitAs($_DONE);
  catchEvent($level).emitAs($PROC).emitAs($_DONE);
  catchEvent($meshi).emitAs($PROC).emitAs($_DONE);
  // Notification states
  catchEvent($test).emitAs($reset).setValue(100%).emitAs($s_tes).emitAs($_DONE).emitAs($_BUSY);
  catchEvent($busy).emitAs($reset).setValue(100%).emitAs($s_bus).setValue(0%).emitAs($v_pro).emitAs($_DONE);
  catchEvent($print).emitAs($reset).setValue(100%).emitAs($s_bus);
  catchEvent($heati).emitAs($reset).setValue(100%).emitAs($s_hea);
  catchEvent($off).emitAs($SHUT).emitAs($_DONE);
  catchEvent($stand).emitAs($reset).setValue(100%).emitAs($s_std).emitAs($_DONE).emitAs($_BUSY);
  catchEvent($stdby).emitAs($_PROC);
  catchEvent($pause).emitAs($reset).setValue(100%).emitAs($s_pau);
  // Animation Value State
  catchEvent($booti).emitAs($v_boo).emitAs($_SHUT).emitAs($_DONE);
  catchEvent($disco).emitAs($v_dis);
  catchEvent($toggl).emitAs($v_tog).emitAs($v_lig).emitAs($_DONE);
  catchEvent($light).emitAs($v_lig).emitAs($_DONE);
  catchEvent($progr).emitAs($v_pro);
  catchEvent($color).emitAs($v_col).emitAs($_SHUT).emitAs($_DONE);
  catchEvent($brigh).emitAs($v_bri).setValue(100%).emitAs($v_tog).emitAs($_SHUT).emitAs($_DONE);
  catchEvent($busy).emitAs($BUSY);
  catchEvent($print).emitAs($BUSY);
  catchEvent($done).emitAs($DONE).emitAs($_BUSY);
  catchEvent($error).setValue(100%).emitAs($v_err);
  // clear done or busy
  catchEvent($_BOOT).setValue(0%).emitAs($v_boo);
  catchEvent($BOOT).setValue(100%).emitAs($v_boo);
  catchEvent($_DONE).setValue(0%).emitAs($v_don);
  catchEvent($DONE).setValue(100%).emitAs($v_don);
  catchEvent($_BUSY).setValue(0%).emitAs($v_bus);
  catchEvent($BUSY).setValue(100%).emitAs($v_bus);
  catchEvent($_PROC).setValue(0%).emitAs($v_prc);
  catchEvent($PROC).setValue(100%).emitAs($v_prc);
  catchEvent($_SHUT).setValue(0%).emitAs($v_shu);
  catchEvent($SHUT).setValue(100%).emitAs($v_shu);

  catchEvent($reset).setValue(0%).emitAs($v_err).emitAs($v_dis).emitAs($v_boo).emitAs($v_prc).emitAs($v_shu).emitAs($s_tes).emitAs($s_bus).emitAs($s_hea).emitAs($s_std).emitAs($s_pau);

  var notification_state_testing = genSmoothOut(genLastEventParam($s_tes), &SMOOTING_TRANSITION);
  var notification_state_busy = genSmoothOut(genLastEventParam($s_bus), &SMOOTING_TRANSITION);
  var notification_state_heating = genSmoothOut(genLastEventParam($s_hea), &SMOOTING_TRANSITION);
  var notification_state_standby = genSmoothOut(genLastEventParam($s_std), &SMOOTING_TRANSITION);
  var notification_state_pause = genSmoothOut(genLastEventParam($s_pau), &SMOOTING_TRANSITION);

  var value_booting = genSmoothOut(genLastEventParam($v_boo), &SMOOTING_TRANSITION);
  var value_disconnected = genSmoothOut(genLastEventParam($v_dis), &SMOOTING_TRANSITION);
  var value_progress = genLastEventParam($v_pro);
  var value_color = genSmoothOut(genLastEventParam($v_col), &SMOOTING_TRANSITION);
  var value_brightness = genSmoothOut(mapValue(genLastEventParam($v_bri), 5%, 100%, 0%, 100%), &SMOOTING_TRANSITION);
  var value_busy = genSmoothOut(genLastEventParam($v_bus), &SMOOTING_TRANSITION);
  var value_light = genSmoothOut(genLastEventParam($v_lig), &SMOOTING_TRANSITION);
  var value_done = genSmoothOut(genLastEventParam($v_don), &SMOOTING_TRANSITION);
  var value_error = genSmoothOut(genLastEventParam($v_err), &SMOOTING_TRANSITION);
  var value_processing = genSmoothOut(genLastEventParam($v_prc), &SMOOTING_TRANSITION);
  var value_shutdown = genSmoothOut(genLastEventParam($v_shu), &SMOOTING_TRANSITION);
  // State machine
// User Animations on portal
siftSegments({ segment($porta); }, {
  addLayer(0s, Infinity, {
    var show_standby = mapValue(value_busy, 0%, 80%, 100%, 0%);
    var show_busy = mapValue(value_busy, 20%, 100%, 0%, 100%);
    // Printer test animation
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(1s, #ff0000).animFill(1s, #00ff00).animFill(1s, #0000ff).animFill(1s, #ff00ff).animFill(1s, #ffff00).animFill(1s, #00ffff).animFill(1s, #ffffff).animFill(1s, #000000));
      addDrawing(0s, Infinity, animPlasmaShot(8s, #ffffff, 1%));
    }).modifyBrightness(&notification_state_testing);
    // Printer states colors
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, &value_color));
    }).modifyBrightness(&notification_state_busy);
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, &value_color));
    }).modifyBrightness(&notification_state_heating);
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, &value_color));
    }).modifyBrightness(&notification_state_standby);
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, &value_color));
    }).modifyBrightness(&notification_state_pause);
    // Mask
    scaLayer(0s, Infinity, {
      addLayer(0s, Infinity, {
        addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
        scaDrawing(0s, Infinity, animColorRoll(7s, #ffffff, #000000));
        scaDrawing(0s, Infinity, animColorRoll(-9s, #ffffff, #000000));
      }).modifyBrightness(&show_busy);
      addLayer(0s, Infinity, {
        addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
        subDrawing(0s, Infinity, animPlasmaShot(60s, #ffffff, 3%));
        subDrawing(0s, Infinity, animPlasmaShot(-40s, #ffffff, 3%));
      }).modifyBrightness(&show_standby);
    });
  }).modifyBrightness(&value_brightness);
});
// Notification States
siftSegments({ segment($front); }, {
  // Printer test
  addLayer(0s, Infinity, {
    addDrawing(0s, Infinity, animFill(1s, #ff0000).animFill(1s, #00ff00).animFill(1s, #0000ff).animFill(1s, #ff00ff).animFill(1s, #ffff00).animFill(1s, #00ffff).animFill(1s, #ffffff).animFill(1s, #000000));
    addDrawing(0s, Infinity, animPlasmaShot(8s, #ffffff, 1%));
  }).modifyBrightness(&notification_state_testing);
  // Printer busy notification
  addLayer(0s, Infinity, {
    var show_heating = mapValue(value_progress, 0%, 0.01%, 100%, 0%);
    var show_progress = mapValue(value_progress, 0.01%, 0.02%, 0%, 100%);
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animColorRoll(5s, #ff2200, #ff2200));
      scaDrawing(0s, Infinity, animColorRoll(7s, #ffffff, #000000));
      scaDrawing(0s, Infinity, animColorRoll(-9s, #ffffff, #000000));
    }).modifyBrightness(&show_heating);
    addLayer(0s, Infinity, {
      var progress_time = mapValue(value_progress, 0%, 100%, 0s, 100s);
      var backgroud = addValues(#060606, mulValues(value_color, #030303));
      addLayer(0s, Infinity, {
        addDrawing(0s, Infinity, animLoadingBar(100s, &value_color, &backgroud).animFill(Infinity, &value_color));
      }).modifyTimeSet(&progress_time);
      scaLayer(0s, Infinity, {
        addDrawing(0s, Infinity, animFill(Infinity, #b8b8b8));
        addDrawing(0s, Infinity, animPlasmaShot(10s, #ffffff, 5%));
      });
    }).modifyBrightness(&show_progress);
  }).modifyBrightness(&notification_state_busy);
  // Printer heating notification
  addLayer(0s, Infinity, {
    addDrawing(0s, Infinity, animColorRoll(5s, #ff2200, #ff2200));
    scaDrawing(0s, Infinity, animColorRoll(7s, #ffffff, #000000));
    scaDrawing(0s, Infinity, animColorRoll(-9s, #ffffff, #000000));
  }).modifyBrightness(&notification_state_heating);
  // Printer standby notification
  addLayer(0s, Infinity, {
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, &value_color));
    }).modifyBrightness(3%);
  }).modifyBrightness(&notification_state_standby);
  // Printer paused notification
  addLayer(0s, Infinity, {
    var progress_time = mapValue(value_progress, 0%, 100%, 0s, 95s);
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animLoadingBar(100s, &value_color, #ffffff).animFill(Infinity, &value_color));
    }).modifyTimeSet(&progress_time);
    scaDrawing(0s, Infinity, animFade(2s, #000000, #ffffff).animFill(0.5s, #ffffff).animFade(2s, #ffffff, #000000));
  }).modifyBrightness(&notification_state_pause);
});
// Printer is processing
  siftCanvases({ canvas($FRONT, $__STR, 1s); }, {
    subLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&value_processing);
  });
  siftSegments({ segment($front); }, {
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animColorGradient5(5s, #000000, #000000, #ffffff, #000000, #000000, 100%, 100%));
    }).modifyBrightness(&value_processing);
  });
// Printer paused
  siftCanvases({ canvas($PORTA, $__STR, 1s); }, {
    subLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&notification_state_pause);
  });
  siftSegments({ segment($porta); }, {
    addLayer(0s, Infinity, {
      var progress_time = mapValue(value_progress, 0%, 100%, 0s, 95s);
      addLayer(0s, Infinity, {
        addDrawing(0s, Infinity, animLoadingBar(100s, &value_color, #ffffff).animFill(Infinity, &value_color));
      }).modifyTimeSet(&progress_time);
      scaDrawing(0s, Infinity, animFade(2s, #000000, #ffffff).animFill(0.5s, #ffffff).animFade(2s, #ffffff, #000000));
    }).modifyBrightness(&notification_state_pause);
  });
// Booting
  siftSegments({ segment($front); segment($por_L); segment($por_T); segment($por_R); }, {
    subLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&value_booting);
  });
  addLayer(0s, Infinity, {
    siftSegments({ segment($porta); segment($front); }, {
      addLayer(0s, 10s, {
        addDrawing(0s, 10s, animColorGradient5(6s, #000000, &value_color, &value_color, &value_color, &value_color, 100%, 100%).animFade(0.1s, &value_color, #ffffff).animFill(1s, #ffffff).animFade(2.9s, #ffffff, #000000));
        scaDrawing(0s, 10s, animLoadingBar(6s, #ffffff, #000000).animNone(Infinity));
      }).modifyFadeOut(1s);
    });
    siftSegments({ segment($por_T); }, {
      addDrawing(10s, Infinity, animFade(1s, #000000, #ffffff).animFade(1s, #ffffff, #000000));
    });
    siftSegments({ segment($front); }, {
      addDrawing(10s, Infinity, animPlasmaShot(1s, #c9c9c9, 25%).animPlasmaShot(-1s, #c9c9c9, 25%).animNone(2s));
    });
  }).modifyBrightness(&value_booting);
// Print done
  siftSegments({ segment($front); segment($por_L); segment($por_T); segment($por_R); }, {
    subLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&value_done);
  });
  interactive<0x01>(0s, Infinity, $done, {
    addLayer(0s, 10s, {
      siftSegments({ segment($porta); segment($front); }, {
        addDrawing(0s, Infinity, animRainbow(5s, 60%));
        addDrawing(1.1s, 1s, animPlasmaShot(1s, #ffffff, 10%));
        addDrawing(1.4s, 1s, animPlasmaShot(-1s, #ffffff, 10%));
        addDrawing(1.8s, 1s, animPlasmaShot(1s, #ffffff, 10%));
      });
    }).modifyFadeIn(1s).modifyFadeOut(1.5s);
    addLayer(9.5s, Infinity, {
      siftSegments({ segment($por_T); }, {
        addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
      });
      siftSegments({ segment($front); }, {
        addDrawing(0s, Infinity, animRainbow(5s, 60%));
      });
    }).modifyFadeIn(1.5s).modifyBrightness(&value_done);
  });
// Connected animation
  interactive<0x01>(0s, Infinity, $CONNE, {
    scope(0s, 10s, {
      siftSegments({ segment($porta); segment($front); }, {
        addLayer(0.2s, 2s, {
          addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
        }).modifyFadeIn(0.2s).modifyFadeOut(1.5s);
      });
    });
  });
// Error
  siftSegments({ segment($front); segment($por_L); segment($por_T); segment($por_R); }, {
    subLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&value_error);
  });
  addLayer(0s, Infinity, {
    siftSegments({ segment($por_T); }, {
      addDrawing(0s, Infinity, animFade(0.05s, #000000, #ff0000).animFill(0.1s, #ff0000).animFade(0.05s, #ff0000, #000000).animFill(0.2s, #000000).animFade(0.01s, #000000, #ff0000).animFill(2s, #ff0000).animFade(1s, #ff0000, #000000).animFill(1s, #000000));
    });
    siftSegments({ segment($front); }, {
      addDrawing(0s, Infinity, animFade(0.05s, #000000, #ff0000).animFill(0.1s, #ff0000).animFade(0.05s, #ff0000, #000000).animFill(0.2s, #000000).animFade(0.01s, #000000, #ff0000).animFill(2s, #ff0000).animFade(1s, #ff0000, #000000).animFill(1s, #000000));
    });
  }).modifyBrightness(&value_error);
// Shutdown
  siftSegments({ segment($front); segment($por_L); segment($por_T); segment($por_R); }, {
    subLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&value_shutdown);
  });
// Disconnected
  siftSegments({ segment($front); }, {
    addLayer(0s, Infinity, {
      interactive<0x01>(0s, Infinity, $disco, {
        addDrawing(0s, 12s, animPlasmaShot(1s, #c9c9c9, 25%).animPlasmaShot(-1s, #c9c9c9, 25%).animNone(2s));
      });
    }).modifyBrightness(&value_disconnected);
  });
// Lightning Button
  siftSegments({ segment($por_L); segment($por_T); segment($por_R); }, {
    var scale = mapValue(value_light, 0%, 20%, 100%, 0%);
    var add = value_light;
    scaLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&scale);
    addLayer(0s, Infinity, {
      addDrawing(0s, Infinity, animFill(Infinity, #ffffff));
    }).modifyBrightness(&add);
  });
// Print done
  interactive<0x01>(0s, Infinity, $blink, {
    scope(0s, 10s, {
      siftSegments({ segment($por_L); segment($por_T); segment($por_R); }, {
        scaDrawing(0s, 4s, animFade(0.5s, #ffffff, #000000).animFill(3s, #000000).animFade(0.5s, #000000, #ffffff));
        setDrawing(1s, 2s, animFill(Infinity, #ffffff));
      });
    });
  });
